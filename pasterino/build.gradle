/*
 * Copyright (C) 2018 Peter Kenji Yamanaka
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: "com.android.application"
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: "com.getkeepsafe.dexcount"

//noinspection GroovyMissingReturnStatement
android {
  compileSdkVersion rootProject.ext.maxSdk

  defaultConfig {
    applicationId "com.pyamsoft.pasterino"
    minSdkVersion rootProject.ext.minSdk
    targetSdkVersion rootProject.ext.maxSdk
    versionCode 72
    versionName "1.6.2"

    // For vector drawables
    vectorDrawables.useSupportLibrary = true

    resConfigs "en"
  }

  signingConfigs {
    release {
      storeFile new File(RELEASE_STORE_FILE)
      keyAlias RELEASE_KEY_ALIAS
      keyPassword RELEASE_KEY_PASSWD
      storePassword RELEASE_STORE_PASSWD
    }
  }

  dataBinding {
    enabled true
  }

  buildTypes {
    release {
      signingConfig signingConfigs.release
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile("proguard-android.txt"),
          "proguard-rules.pro"
    }

    debug {
      applicationIdSuffix ".dev"
      versionNameSuffix "-dev"
    }
  }
}

// Kapt flags
kapt {
  correctErrorTypes = true
  useBuildCache = true
  mapDiagnosticLocations = true

  javacOptions {
    option("-Xlint:unchecked")
    option("-Xlint:deprecation")
    option("-Xmaxerrs", 10000)
  }
}

dependencies {
  // Leak Canary
  debugImplementation "com.squareup.leakcanary:leakcanary-support-fragment:$leakCanary"
  debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanary"
  releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanary"
  testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanary"

  // PYDroid
  implementation "com.pyamsoft.pydroid:ui:$pydroid"

  implementation project(":pasterino-base")
  implementation project(":pasterino-main")
  implementation project(":pasterino-service")
}
